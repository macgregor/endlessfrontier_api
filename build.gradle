plugins {
    id "java"
    id "application"
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id 'com.benjaminsproule.swagger' version '1.0.0'
    id "org.flywaydb.flyway" version "5.0.7"
    id "idea"
}

version = '0.0.1'
group = 'com.macgregor'
sourceCompatibility = 1.8
targetCompatibility = '1.8'
mainClassName = 'com.macgregor.ef.EndlessFrontierAPI'

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

ext {
    dropwizardVersion = '1.2.3'
    dropwizardSwaggerVersion = '1.2.3-3' //uses dropwizard 1.2.3
    apiVersion = 'v1'
}

// prevent project build from creating /out dir with duplicate build files
// see https://stackoverflow.com/questions/45174989/building-with-intellij-2017-2-out-directory-duplicates-files-in-build-director
idea {
    module {
        outputDir file('build/classes/main')
        testOutputDir file('build/classes/test')
    }
}
if(project.convention.findPlugin(JavaPluginConvention)) {
    // Change the output directory for the main and test source sets back to the old path
    sourceSets.main.output.classesDir = new File(buildDir, "classes/main")
    sourceSets.test.output.classesDir = new File(buildDir, "classes/test")
}

dependencies {
    compile ("io.dropwizard:dropwizard-core:${dropwizardVersion}",
             "io.dropwizard:dropwizard-db:${dropwizardVersion}",
             "io.dropwizard:dropwizard-hibernate:${dropwizardVersion}",
             "io.dropwizard:dropwizard-validation:${dropwizardVersion}",
             "io.dropwizard-bundles:dropwizard-redirect-bundle:1.0.5",
             "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.4",
             "org.codehaus.woodstox:woodstox-core-asl:4.4.1",
             "com.h2database:h2:1.4.196",
             "org.apache.commons:commons-lang3:3.7"
    )
    compile("com.smoketurner:dropwizard-swagger:${dropwizardSwaggerVersion}"){
        exclude group: 'org.reflections', module: 'reflections'
    }

    // see https://groups.google.com/forum/#!topic/dropwizard-user/I0K4e0APudY
    compile("org.reflections:reflections:0.9.10"){
        exclude group: "com.google.guava", module: "guava"
        exclude group: "com.google.code.findbugs", module: "annotations"
    }
    testCompile(
            'junit:junit:4.12',
            "io.dropwizard:dropwizard-testing:${dropwizardVersion}",
            "io.dropwizard:dropwizard-client:${dropwizardVersion}",
            "org.mockito:mockito-core:2.15.0"
    )
    testCompile('org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:2.25.1'){
        exclude group: "javax.servlet", module: "javax.servlet-api"
        exclude group: "junit", module: "junit"
    }

}

clean.doFirst {
    delete "${rootDir}/out/"
}

shadowJar {
    mergeServiceFiles()
    mainClassName = 'com.macgregor.ef.EndlessFrontierAPI'
    destinationDir = new File("${project.buildDir}") //weird quirk, requires a file not a string
    baseName = "${rootProject.name}"
    classifier = null
    version = null
}

// gives us better output when running tests
tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "skipped", "passed", "failed"
        showStandardStreams true
    }
    maxParallelForks = 2
}

/*
 * Create or update h2 database
 */
flyway {
    url = 'jdbc:h2:file:./build/efdb'
    user = 'sa'
}

// heroku build runs ./gradlew stage for its build
task stage(dependsOn: ['build', 'clean', 'shadowJar', 'flywayMigrate']) {}
build.mustRunAfter clean