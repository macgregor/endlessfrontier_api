plugins {
    id "java"
    id "application"
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id 'com.benjaminsproule.swagger' version '1.0.0'
    id "org.flywaydb.flyway" version "5.0.7"
    id "idea"
}

version = '0.0.1'
group = 'com.macgregor'
sourceCompatibility = 1.8
targetCompatibility = '1.8'
mainClassName = 'com.macgregor.ef.RestJava'

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

ext {
    dropwizardVersion = '1.2.3'
    dropwizardSwaggerVersion = '1.2.3-3' //uses dropwizard 1.2.3
    apiVersion = 'v1'
}

dependencies {
    compile ("io.dropwizard:dropwizard-core:${dropwizardVersion}",
             "io.dropwizard:dropwizard-db:${dropwizardVersion}",
             "io.dropwizard:dropwizard-hibernate:${dropwizardVersion}",
             "io.dropwizard:dropwizard-validation:${dropwizardVersion}",
             "com.h2database:h2:1.4.196"
    )
    compile("com.smoketurner:dropwizard-swagger:${dropwizardSwaggerVersion}"){
        exclude group: 'org.reflections', module: 'reflections'
    }

    // see https://groups.google.com/forum/#!topic/dropwizard-user/I0K4e0APudY
    compile("org.reflections:reflections:0.9.10"){
        exclude group: "com.google.guava", module: "guava"
        exclude group: "com.google.code.findbugs", module: "annotations"
    }
    testCompile(
            'junit:junit:4.12',
            "io.dropwizard:dropwizard-testing:${dropwizardVersion}",
            "io.dropwizard:dropwizard-client:${dropwizardVersion}",
            "org.mockito:mockito-core:2.15.0"
    )

}

shadowJar {
    mergeServiceFiles()
    mainClassName = 'com.macgregor.ef.RestJava'
    destinationDir = new File("${project.buildDir}") //weird quirk, requires a file not a string
    baseName = "${rootProject.name}"
    classifier = null
    version = null
}

// gives us better output when running tests
tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}

//defines the swagger project used by generateSwaggerDocumentation
swagger {
    apiSource {
        springmvc = false
        locations = ['com.macgregor.ef.resource'] //package containing swagger swagger @Api annotations
        schemes = ['https']
        host = "${rootProject.name}.herokuapp.com"
        swaggerDirectory = "${projectDir}/docs/"
        basePath = '/'
        info {
            title = "${rootProject.name}"
            version = "${apiVersion}"
            contact {
                email = 'matthew.m.stratton@gmail.com'
                name = 'Matthew Stratton'
                url = 'https://github.com/macgregor/'
            }
            license {
                name='MIT'
                url='https://opensource.org/licenses/MIT'
            }
        }
    }
}

/*
 * converts swagger.json produced by generateSwaggerDocumentation into a hostable html5 website
 * requires spectacle be installed: `npm install -g spectacle-docs`
 *
 * input: ${projectDir}/docs/swagger.json
 * output dir: ${projectDir}/docs/
 */
task generateDocs(type: Exec, dependsOn: generateSwaggerDocumentation) {
    executable "spectacle"
    args "-t", "${projectDir}/docs/","${projectDir}/docs/swagger.json"
}

/*
 * Create or update h2 database
 */
flyway {
    url = 'jdbc:h2:file:./persondb'
    user = 'sa'
}

// heroku build runs ./gradlew stage for its build
task stage(dependsOn: ['build', 'clean', 'shadowJar', 'flywayMigrate']) {}
build.mustRunAfter clean